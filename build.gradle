plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.0.0-beta11"
    id 'maven-publish'
}

apply plugin: 'java'

group = 'com.inotsleep'
version = '2.0.0'

repositories {
    mavenCentral()
}

configurations {
    shadow {
        transitive = false
    }

    transitiveShadow
}

dependencies {
    shadow project(path: ":core", configuration: "shadow")
}

def isCI   = System.getenv("CI") == "true"
def gitRef = System.getenv("GITHUB_REF") ?: ""

if (isCI && !gitRef.startsWith("refs/tags/") && !version.endsWith("-SNAPSHOT")) {
    version = "${version}-SNAPSHOT"
}

def extBuildClassifier = (findProperty("buildClassifier") as String) ?: ""

shadowJar {
    configurations = [project.configurations.shadow, project.configurations.transitiveShadow]
    dependsOn(tasks.named('jar'))

    archiveBaseName = "INSUtils"
    archiveClassifier = extBuildClassifier
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            artifact shadowJar

            groupId = project.group
            artifactId = "insutils"
            version = project.version

        }
    }

    repositories {
        maven {
            name = "inotsleep-nexus"
            url = uri(
                    version.endsWith("-SNAPSHOT")
                    ? "https://nexus.inotsleep.com/repository/maven-snapshots/"
                    : "https://nexus.inotsleep.com/repository/maven-releases/"
            )
            credentials {
                username = System.getenv("NEXUS_USER")
                password = System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}

tasks.named("jar") {
    enabled = false
}

tasks.named("assemble") {
    dependsOn(tasks.named("shadowJar"))
}