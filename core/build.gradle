plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.0.0-beta11"
    id 'maven-publish'
}

apply plugin: 'java'

group = property("group")
version = property("version")

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        url = "https://jitpack.io/"
    }
}

configurations {
    shadow
}

dependencies {
    shadow project(path: ":api", configuration: "shadow")
    shadow "org.bstats:bstats-bukkit:3.1.0"
    shadow "org.bstats:bstats-bungeecord:3.1.0"
    shadow "org.snakeyaml:snakeyaml-engine:2.9"
    shadow "com.github.Querz:NBT:6.1"

    compileOnly "org.xerial:sqlite-jdbc:3.43.2.1"
    compileOnly "com.google.guava:guava:12+"
    compileOnly 'me.filoghost.holographicdisplays:holographicdisplays-api:3.0.0'
    compileOnly "com.github.decentsoftware-eu:decentholograms:2.8.9"
    compileOnly "org.apache.logging.log4j:log4j-api:2.24.3"
    compileOnly 'io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT'
    compileOnly "com.google.code.gson:gson:2.13.2"
    compileOnly 'net.md-5:bungeecord-api:1.21-R0.5-SNAPSHOT'
}

shadowJar {
    configurations = [project.configurations.shadow]
    dependsOn(tasks.named('jar'))

    archiveClassifier = ""

    relocate("org.bstats", "com.inotsleep.insutils.internal.relocated.org.bstats")
    relocate("org.snakeyaml", "com.inotsleep.insutils.internal.relocated.org.snakeyaml")
    relocate("net.querz", "com.inotsleep.insutils.internal.relocated.net.querz")
}

tasks.named("build") {
    dependsOn(shadowJar)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('bungee.yml') {
        expand props
    }
}